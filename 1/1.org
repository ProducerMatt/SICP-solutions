# ORG-BABEL DEFAULTS
#+PROPERTY: header-args :tangle no :noeval :exports both :cache yes :results output wrap :noweb no-export :comments noweb :session default
#
# For the actual answers their dependencies use a header like this:
# #+BEGIN_SRC scheme -n :eval no-export :tangle yes :exports both
# For their dependencies:
# #+BEGIN_SRC scheme :eval no-export :tangle yes :results silent

#+auto_tangle: t

#+PANDOC_OPTIONS: standalone:t
# FIXME: this doesn't appear to do anything. Be sure to run pandoc with -s

#+LATEX_CMD: xelatex
#+LATEX_HEADER: \setmonofont[Mapping=tex-text,Scale=MatchLowercase]{FiraMono-Regular}
#+LATEX_HEADER: \listfiles
# listfiles causes latex to make a list of all files used during processing

#+title: SICP Chapter 1
#+AUTHOR: ProducerMatt

* HOW THIS DOCUMENT IS MADE
**TODO**
#+NAME: testing
#+BEGIN_SRC scheme -n :exports both :eval no-export :results value
(define (foo a b)
  (+ a (* 2 b)))

(foo 5 3)
#+END_SRC

#+RESULTS[57407d275aacd484646294a75251646c910ae5fc]: testing
:results:
11
:end:

^ Dynamically evaluated when you press "enter" on the ~BEGIN_SRC~ block!

*** Also consider:
- ~:results output~ for what the code prints
- ~:exports code~ or ~:exports results~ to just get one or the other

\(a + (\pi \times b)\) <~ inline Latex btw :)

*** Current command for conversion
#+BEGIN_SRC sh :noeval
pandoc --from org --to latex 1.org -o 1.tex -s; xelatex 1.tex
#+END_SRC

** Helper for org-mode tables

#+NAME: transpose-list
#+BEGIN_SRC scheme :eval no-export :tangle yes :results silent
(define (transpose-list l)
  (map (λ (i) (list i)) l))
#+end_src
#+NAME: print-as-rows
#+BEGIN_SRC scheme :eval no-export :tangle yes :results silent
(define (p-nl a)
  (display a)
  (newline))
(define (print-spaced args)
  (let ((a (car args))
        (d (cdr args)))
    (if (null? d)
        (p-nl a)
        (begin (display a)
               (display " ")
               (print-spaced d)))))
(define (print-as-rows . args)
  (let ((a (car args))
        (d (cdr args)))
    (if (list? a)
        (if (= 1 (length args))
            (apply print-as-rows a)
            (print-spaced a))
        (p-nl a))
    (if (null? d)
        '()
        (apply print-as-rows d))))
#+end_src
* Exercise 1.1
** Q
Below is a sequence of expressions. What is the result printed by the
interpreter in response to each expression? Assume that the sequence is to be
evaluated in the order in which it is presented.
** A
#+BEGIN_SRC scheme -n :results none
10 ;; 10
(+ 5 3 4) ;; 12
(- 9 1) ;; 8
(/ 6 2) ;; 3
(+ (* 2 4) (- 4 6)) ;; 6
(define a 3) ;; a=3
(define b (+ a 1)) ;; b=4
(+ a b (* a b)) ;; 19
(= a b) ;; false
(if (and (> b a) (< b (* a b)))
    b
    a) ;; 4
(cond ((= a 4) 6)
      ((= b 4) (+ 6 7 a))
      (else 25)) ;; 16
(+ 2 (if (> b a) b a)) ;; 6
(* (cond ((> a b) a)
         ((< a b) b)
         (else -1))
   (+ a 1)) ;; 16
#+END_SRC

* Exercise 1.2
** Q
Translate the following expression into prefix form:
\begin{equation}
  \frac{5 + 2 + (2 - 3 - (6 + \frac{4}{5})))}
            {3(6 - 2)(2 - 7)}
\end{equation}
** A
#+NAME: EX1-2
#+BEGIN_SRC scheme :eval no-export :exports both :results value
(/ (+ 5 2 (- 2 3 (+ 6 (/ 4 5))))
   (* 3 (- 6 2) (- 2 7)))
#+END_SRC

#+RESULTS[7039c32447725d91001cae297dc9040b72a855ce]: EX1-2
:results:
1/75
:end:


* Exercise 1.3
** Text
#+NAME: square
#+BEGIN_SRC scheme :eval no-export :results silent :tangle yes
(define (square x)
  (* x x))
#+END_SRC
** Q
Define a procedure that takes three numbers as arguments and returns the sum of
the squares of the two larger numbers.
** A
#+NAME: EX1-3
#+BEGIN_SRC scheme -n :eval no-export :tangle yes :exports both :results value table
<<square>>
(define (sum-square x y)
  (+ (square x) (square y)))
(define (square-2of3 a b c)
  (cond ((and (>= a b) (>= b c)) (sum-square a b))
        ((and (>= a b) (> c b)) (sum-square a c))
        ((and (> b a) (>= c a)) (sum-square b c))
        (else "This shouldn't happen")))

 (list (square-2of3 7 5 3)
       (square-2of3 7 3 5)
       (square-2of3 3 5 7))

#+END_SRC

#+RESULTS[581e1faead1b527d69adae4c0b78d6faee8b4e70]: EX1-3
:results:
| 74 | 74 | 74 |
:end:

* Exercise 1.4
** Q
Observe that our model of evaluation allows for combinations whose operators are
compound expressions. Use this observation to describe the behavior of the
following procedure:

#+NAME: a-plus-abs-b
#+BEGIN_SRC scheme
(define (a-plus-abs-b a b)
  ((if (> b 0) + -) a b))
#+END_SRC

** A
This code accepts the variables ~a~ and ~b~, and if ~b~ is positive, it adds ~a~
and ~b~. However, if ~b~ is zero or negative, it subtracts them. This decision
is made by using the ~+~ and ~-~ procedures as the results of an if expression,
and then evaluating according to the results of that expression. This is in
contrast to a language like Python, which would do something like this:

#+BEGIN_SRC python :noeval :tangle no
if b > 0: a + b
else: a - b
#+END_SRC

* Exercise 1.5
** Q
Ben Bitdiddle has invented a test to determine whether the interpreter he is
faced with is using applicative-order evaluation or normal-order evaluation. He
defines the following two procedures:

#+BEGIN_SRC scheme :noeval :tangle no
(define (p) (p))

(define (test x y)
  (if (= x 0)
      0
      y))
#+END_SRC
Then he evaluates the expression

#+BEGIN_SRC scheme :noeval :tangle no
(test 0 (p))
#+END_SRC

What behavior will Ben observe with an interpreter that uses applicative-order
evaluation? What behavior will he observe with an interpreter that uses
normal-order evaluation? Explain your answer. (Assume that the evaluation rule
for the special form if is the same whether the interpreter is using normal or
applicative order: The predicate expression is evaluated first, and the result
determines whether to evaluate the consequent or the alternative expression.)

** A
In either type of language, src_scheme{(define (p) (p))} is an infinite
loop. However, a normal-order language will encounter the special form, return
~0~, and never evaluate ~(p)~. An applicative-order language evaluates the
arguments to src_scheme{(test 0 (p))}, thus triggering the infinite
loop.

* Exercise 1.6
** Text code
#+NAME: abs
#+BEGIN_SRC scheme :eval no-export :tangle yes :results silent
(define (abs x)
  (if (< x 0)
	  (- x)
	  x))
#+END_SRC
#+NAME: average
#+BEGIN_SRC scheme :eval no-export :tangle yes :results silent
(define (average x y)
  (/ (+ x y) 2))
#+END_SRC
#+NAME: txt-sqrt
#+BEGIN_SRC scheme :eval no-export :tangle yes :results silent
<<average>>
(define (improve guess x)
  (average guess (/ x guess)))

<<square>>
<<abs>>
(define (good-enough? guess x)
  (< (abs (- (square guess) x)) 0.001))

(define (sqrt-iter guess x)
  (if (good-enough? guess x)
      guess
      (sqrt-iter (improve guess x) x)))

(define (sqrt x)
  (sqrt-iter 1.0 x))
#+END_SRC

** Q
Exercise 1.6: Alyssa P. Hacker doesn’t see why if needs to be provided as a
special form. “Why can’t I just define it as an ordinary procedure in terms of
cond?” she asks. Alyssa’s friend Eva Lu Ator claims this can indeed be done, and
she defines a new version of if:

#+BEGIN_SRC scheme :noeval :tangle no
(define (new-if predicate
                then-clause
                else-clause)
  (cond (predicate then-clause)
        (else else-clause)))
#+END_SRC
Eva demonstrates the program for Alyssa:

#+BEGIN_SRC scheme :noeval :tangle no
(new-if (= 2 3) 0 5)
;; => 5

(new-if (= 1 1) 0 5)
;; => 0
#+END_SRC

Delighted, Alyssa uses new-if to rewrite the square-root program:

#+BEGIN_SRC scheme :noeval :tangle no
(define (sqrt-iter guess x)
  (new-if (good-enough? guess x)
          guess
          (sqrt-iter (improve guess x) x)))
#+END_SRC

What happens when Alyssa attempts to use this to compute square roots? Explain.

** A
Using Alyssa's ~new-if~ leads to an infinite loop because the recursive call to
~sqrt-iter~ is evaluated before the actual call to ~new-if~. This is because
~if~ and ~cond~ are special forms that change the way evaluation is handled;
whichever branch is chosen leaves the other branches unevaluated.

* Exercise 1.7
** Text
#+NAME: mean-square
#+BEGIN_SRC scheme :eval no-export :tangle yes :results silent
(define (mean-square x y)
  (average (square x) (square y)))
#+END_SRC
** Q
The good-enough? test used in computing square roots will not be very effective
for finding the square roots of very small numbers. Also, in real computers,
arithmetic operations are almost always performed with limited precision. This
makes our test inadequate for very large numbers. Explain these statements, with
examples showing how the test fails for small and large numbers. An alternative
strategy for implementing good-enough? is to watch how guess changes from one
iteration to the next and to stop when the change is a very small fraction of
the guess. Design a square-root procedure that uses this kind of end test. Does
this work better for small and large numbers?
** A
The current method has decreasing accuracy with smaller numbers. Notice the
steady divergence from correct answers here (should be 0.1s getting smaller and smaller):
#+NAME: EX1-7-t1
#+BEGIN_SRC scheme :eval no-export :tangle no :exports both :results value table
<<transpose-list>>
<<txt-sqrt>>
(transpose-list (map sqrt '(0.01 0.0001 0.000001 0.00000001 0.0000000001)))
#+END_SRC

#+RESULTS[57d147a092109f331824bacee7632f948edac4d5]: EX1-7-t1
:results:
|  0.10032578510960605 |
|  0.03230844833048122 |
| 0.031260655525445276 |
|  0.03125010656242753 |
|  0.03125000106562499 |
:end:

And for larger numbers, an infinite loop will eventually be reached. \(10^{12}\)
can resolve, but \(10^{13}\) cannot.

#+BEGIN_SRC scheme :eval no-export :tangle no :exports both :results value
<<txt-sqrt>>
(sqrt 1000000000000)
#+END_SRC

#+RESULTS[452bf3da7286d9fc1e3d621aeb715eafef650536]:
:results:
1000000.0
:end:

My original answer was this, which compares the previous iteration until the new
and old are within an arbitrary \(dx\).

#+NAME: inferior-good-enough
#+BEGIN_SRC scheme :tangle yes
<<txt-sqrt>>
(define (inferior-good-enough? guess lastguess)
  (<=
   (abs (-
         (/ lastguess guess)
         1))
   0.0000000000001)) ; dx
(define (new-sqrt-iter guess x lastguess) ;; Memory of previous value
  (if (inferior-good-enough? guess lastguess)
      guess
      (new-sqrt-iter (improve guess x) x guess)))
(define (new-sqrt x)
  (new-sqrt-iter 1.0 x 0))
#+end_src

This solution can correctly find small and large numbers:
#+BEGIN_SRC scheme :eval no-export :tangle no :exports both :results value
<<inferior-good-enough>>
(new-sqrt 10000000000000)
#+END_SRC

#+RESULTS[68a129e63924b52600a4964626d31b776d79fed2]:
:results:
3162277.6601683795
:end:

#+NAME: EX1-7-t2
#+BEGIN_SRC scheme :eval no-export :tangle no :exports both :results value table drawer
<<transpose-list>>
<<inferior-good-enough>>
(transpose-list (map new-sqrt '(0.01 0.0001 0.000001 0.00000001 0.0000000001)))
#+end_src

#+RESULTS[e7b1a787e50f3cca05cce883961f20552fb63151]: EX1-7-t2
:results:
|                   0.1 |
|                  0.01 |
|                 0.001 |
| 9.999999999999999e-05 |
| 9.999999999999999e-06 |
:end:


However, I found this solution online that isn't just simpler but automatically
reaches the precision limit of the system:

#+NAME: new-good-enough
#+BEGIN_SRC scheme :eval no-export :results silent
<<txt-sqrt>>
(define (best-good-enough? guess x)
   (= (improve guess x) guess))
#+END_SRC

#+NAME: EX1-7-t3
#+BEGIN_SRC scheme :eval no-export :tangle no :exports both :results value table
<<transpose-list>>
<<new-good-enough>>
(define (best-sqrt-iter guess x)
  (if (best-good-enough? guess x)
      guess
      (best-sqrt-iter (improve guess x) x)))

(define (best-sqrt x)
  (best-sqrt-iter 1.0 x))

(transpose-list (map best-sqrt '(0.01 0.0001 0.000001 0.00000001 0.0000000001)))
#+end_src

#+RESULTS[815b533a404dbe62c2e92029a6b69103f411372e]: EX1-7-t3
:results:
|                   0.1 |
|                  0.01 |
|                 0.001 |
| 9.999999999999999e-05 |
| 9.999999999999999e-06 |
:end:

* Exercise 1.8
** Q
Newton’s method for cube roots is based on the fact that if y is an
approximation to the cube root of x, then a better approximation is given by the
value:
\begin{equation}
\frac{\frac{x}{y^2} + 2y}{3}
\end{equation}
Use this formula to implement a cube-root procedure analogous to the square-root
procedure. (In 1.3.4 we will see how to implement Newton’s method in general as
an abstraction of these square-root and cube-root procedures.)
** A1
My first attempt works, but needs an arbitrary limit to stop infinite loops:
#+NAME: EX1-8-A1
#+BEGIN_SRC scheme :eval no-export :tangle no :exports both :results value table
<<square>>
(define (cb-good-enough? guess x)
  (= (cb-improve guess x) guess))
(define (cb-improve guess x)
  (/
   (+
    (/ x (square guess))
    (* guess 2))
   3))
(define (cbrt-iter guess x counter)
  (if (or (cb-good-enough? guess x) (> counter 100))
      guess
      (begin
        (cbrt-iter (cb-improve guess x) x (+ 1 counter)))))
(define (cbrt x)
  (cbrt-iter 1.0 x 0))

(let ((testvals (list 7 32 56 100)))
      (map (λ (i) (cons i (cons (cbrt i) #nil))) testvals))
#+end_src

#+RESULTS[b2bd4cffe876e922265b4894e27a1970312fea5e]: EX1-8-A1
:results:
|   7 | 1.912931182772389 |
|  32 | 3.174802103936399 |
|  56 | 3.825862365544778 |
| 100 | 4.641588833612779 |
:end:

However, this will hang on an infinite loop when trying to run src_scheme{(cbrt 100)}.
I speculate it's a floating point precision issue with the "improve"
algorithm. So to avoid it I'll just keep track of the last guess and stop
improving when there's no more change occurring. Also while researching I
discovered that (again due to floating point) src_scheme{(cbrt -2)} loops
forever unless you initialize your guess with a slightly different value, so
let's do 1.1 instead.
** A2
#+NAME: cbrt
#+BEGIN_SRC scheme :eval no-export :tangle yes :exports code :results silent
<<square>>
(define (cb-good-enough? nextguess guess lastguess x)
  (or (= nextguess guess)
      (= nextguess lastguess)))
(define (cb-improve guess x)
  (/
   (+
    (/ x (square guess))
    (* guess 2))
   3))
(define (cbrt-iter guess lastguess x)
  (define nextguess (cb-improve guess x))
  (if (cb-good-enough? nextguess guess lastguess x)
      nextguess
      (cbrt-iter nextguess guess x)))
(define (cbrt x)
  (cbrt-iter 1.1 9999 x))
#+END_SRC
#+BEGIN_SRC scheme :eval no-export :tangle no :exports both :results value table
<<cbrt>>
(let ((testvals (list 7 32 56 100 -2)))
  (map (λ (i) (cons i (cons (cbrt i) #nil))) testvals))
#+END_SRC

#+RESULTS[7450ce5b782e8e88f422b003dc76ad2dbc06d2af]:
:results:
|   7 |   1.912931182772389 |
|  32 |   3.174802103936399 |
|  56 |   3.825862365544778 |
| 100 |   4.641588833612779 |
|  -2 | -1.2599210498948732 |
:end:
